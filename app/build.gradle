apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.dot7.firebase"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        develop {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
            applicationIdSuffix '.dev'
        }
    }

    //ignore debug variant
    variantFilter { variant ->
        def namesFlavor = variant.flavors*.name
        def namesBuildType = variant.buildType*.name

        if (namesBuildType.contains('debug')) {
            variant.ignore = true
        }

        if(namesBuildType.contains("release") && namesFlavor.contains("qa") ){
            variant.ignore = true
        }

        if(namesBuildType.contains("develop") && namesFlavor.contains("prod") ){
            variant.ignore = true
        }
        return variant
    }

    flavorDimensions "app"
    productFlavors {
        qa {
            dimension "app"
            manifestPlaceholders = [AppLabel: "Sento-dev"]
            buildConfigField("String", "sentourl", "\"www.google.com\"")
        }

        prod {
            dimension "app"
            manifestPlaceholders = [AppLabel: "Sento"]
            buildConfigField("String", "sentourl", "\"www.google-prod.com\"")
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    //FIREBASE
    implementation 'com.google.firebase:firebase-auth:19.1.0'
    implementation 'com.google.firebase:firebase-core:17.2.1'
    implementation 'com.google.firebase:firebase-database:19.2.0'
    implementation 'com.google.firebase:firebase-config:19.0.3'
    implementation 'com.google.firebase:firebase-inappmessaging-display:19.0.2'
    implementation 'com.google.firebase:firebase-messaging:20.0.1'
    implementation 'com.google.firebase:firebase-analytics:17.2.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.github.bumptech.glide:glide:4.10.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.10.0'

}
apply plugin: 'com.google.gms.google-services'
